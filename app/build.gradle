plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "com.google.devtools.ksp" version "1.5.31-1.0.0"
}

kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.sawrose.trackr"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        // Enable Room's incremental annotation processor
                        "room.incremental": "true"
                ]
            }
        }
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        lintConfig file('../lint.xml')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        useIR = true

        freeCompilerArgs += [
                "-Xopt-in=kotlin.time.ExperimentalTime",
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    testOptions {
        unitTests.all {
            systemProperty 'robolectric.enabledSdks', '30'

            kover {
                enabled = true
                excludes = [
                        "dagger.hilt.internal.aggregatedroot.codegen.*",
                        "hilt_aggregated_deps.*",
                        "com.adammcneilly.toa.core.di.*",
                        "com.adammcneilly.toa.core.ui.theme.*",
                        ".*ComposableSingletons.*",
                        ".*Hilt.*",
                        ".*BuildConfig.*",
                        ".*_Factory.*",
                ]
            }
        }
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_jdk_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.navigation:navigation-compose:$nav_compose_version"
    implementation "com.google.accompanist:accompanist-insets-ui:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-rc01'
    kapt "androidx.hilt:hilt-compiler:$hilt_compiler_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.room:room-ktx:$room_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "com.google.truth:truth:$truth_version"
    testImplementation "androidx.test:core-ktx:$androidx_test_core_version"
    testImplementation "androidx.test:runner:$androidx_test_runner_version"
    testImplementation "androidx.test.ext:junit:$androidx_test_ext_junit_version"
    testImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    testImplementation "androidx.arch.core:core-testing:$core_testing_version"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // We need this for tests. It should be 'debugImplementation' to put the test manifest in the
    // app package.
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    androidTestImplementation "junit:junit:$junit_version"
    androidTestImplementation "com.google.truth:truth:$truth_version"
    androidTestImplementation "androidx.test:core-ktx:$androidx_test_core_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_ext_junit_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}